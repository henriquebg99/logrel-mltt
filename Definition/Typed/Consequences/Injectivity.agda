{-# OPTIONS --safe #-}

import Definition.Equiv as E
module Definition.Typed.Consequences.Injectivity (equiv : E.Equiv) where

open import Definition.Untyped hiding (wk)
import Definition.Untyped as U
open import Definition.Untyped.Properties

open import Definition.Typed equiv
open import Definition.Typed.Weakening equiv
open import Definition.Typed.Properties equiv
open import Definition.Typed.EqRelInstance equiv
open import Definition.LogicalRelation equiv
open import Definition.LogicalRelation.Irrelevance equiv
open import Definition.LogicalRelation.ShapeView equiv
open import Definition.LogicalRelation.Properties equiv
open import Definition.LogicalRelation.Fundamental.Reducibility equiv
open import Definition.LogicalRelation.Fundamental equiv

open import Tools.Product
import Tools.PropositionalEquality as PE


-- Helper function of injectivity for specific reducible Π-types
injectivity′ : ∀ {F G H E rF lF rH lH lG lE Γ lΠ l}
               ([ΠFG] : Γ ⊩⟨ l ⟩Π Π F ^ rF ° lF  ▹ G ° lG ° lΠ ^ ! ^[ lΠ ] )
             → Γ ⊩⟨ l ⟩ Π F ^ rF ° lF ▹ G ° lG  ° lΠ ^ ! ≡ Π H ^ rH ° lH  ▹ E ° lE  ° lΠ ^ ! ^ [ ! , ι lΠ ] / Π-intr [ΠFG]
             → Γ ⊢ F ≡ H ^ [ rF , ι lF ]
             × rF PE.≡ rH
             × lF PE.≡ lH
             × lG PE.≡ lE
             × Γ ∙ F ^ [ rF , ι lF ] ⊢ G ≡ E ^ [ ! , ι  lG ]
injectivity′ {F₁} {G₁} {H} {E} {lF = lF₁} {Γ = Γ} 
         (noemb (Πᵣ ! lF lG lF≤ lG≤ F G D ⊢F ⊢G A≡A [F] [G] G-ext))
         (Π₌ F′ G′ D′ A≡B [F≡F′] [G≡G′]) =
  let F≡F₁ , rF≡rF₁ , lF≡lF₁ , G≡G₁ , lG≡lG₁ , _ = Π-PE-injectivity (whnfRed* (red D) Πₙ)
      H≡F′ , rH≡rF′ , lH≡lF′ , E≡G′ , lE≡lG′ , _ = Π-PE-injectivity (whnfRed* D′ Πₙ)
      ⊢Γ = wf ⊢F
      [F]₁ = [F] id ⊢Γ
      [F]′ = irrelevance′ (PE.trans (wk-id _) (PE.sym F≡F₁)) [F]₁
      [x∷F] = neuTerm ([F] (step id) (⊢Γ ∙ ⊢F)) (var 0) (var (⊢Γ ∙ ⊢F) here)
                      (refl (var (⊢Γ ∙ ⊢F) here))
      [G]₁ = [G] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G]′ = PE.subst₂ (λ x y → _ ∙ y ^ _ ⊩⟨ _ ⟩ x ^ _)
                       (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                       (PE.sym F≡F₁) [G]₁
      [F≡H]₁ = [F≡F′] id ⊢Γ
      [F≡H]′ = irrelevanceEq″ (PE.trans (wk-id _) (PE.sym F≡F₁))
                              (PE.trans (wk-id _) (PE.sym H≡F′))
                              PE.refl PE.refl 
                              [F]₁ [F]′ [F≡H]₁
      [G≡E]₁ = [G≡G′] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G≡E]′ = irrelevanceEqLift″ (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                                   (PE.trans (wkSingleSubstId _) (PE.sym E≡G′))
                                   (PE.sym F≡F₁) [G]₁ [G]′ [G≡E]₁
  in  PE.subst (λ r → Γ ⊢ _ ≡ _ ^ [ r , ι lF₁ ] ) (PE.sym rF≡rF₁)
        (PE.subst (λ l → Γ ⊢ F₁ ≡ H ^ [ ! , l ] ) (PE.cong ι (PE.sym lF≡lF₁))
          (escapeEq [F]′ [F≡H]′)) ,
     ( PE.trans rF≡rF₁ (PE.sym rH≡rF′) ,
     ( PE.trans lF≡lF₁ (PE.sym lH≡lF′) ,
     ( PE.trans lG≡lG₁ (PE.sym lE≡lG′) ,
        PE.subst (λ r → (_ ∙ _ ^ [ r , _ ] ) ⊢ _ ≡ _ ^ _) (PE.sym rF≡rF₁)
         (PE.subst (λ l → (Γ ∙ F₁  ^ [ ! , ι lF₁ ] ) ⊢ G₁ ≡ E ^ [ ! , l ]) (PE.cong ι  (PE.sym lG≡lG₁))
          (PE.subst (λ l → (Γ ∙ F₁ ^ [ ! , l ] ) ⊢ G₁ ≡ E ^  [ ! , ι lG ]) (PE.cong ι (PE.sym lF≡lF₁))
           (escapeEq [G]′ [G≡E]′))))))

injectivity′ {F₁} {G₁} {H} {E} {lF = lF₁} {Γ = Γ} 
         (noemb (Πᵣ % lF lG lF≤ lG≤ F G D ⊢F ⊢G A≡A [F] [G] G-ext))
         (Π₌ F′ G′ D′ A≡B [F≡F′] [G≡G′]) =
  let F≡F₁ , rF≡rF₁ , lF≡lF₁ , G≡G₁ , lG≡lG₁ , _ = Π-PE-injectivity (whnfRed* (red D) Πₙ)
      H≡F′ , rH≡rF′ , lH≡lF′ , E≡G′ , lE≡lG′ , _ = Π-PE-injectivity (whnfRed* D′ Πₙ)
      ⊢Γ = wf ⊢F
      [F]₁ = [F] id ⊢Γ
      [F]′ = irrelevance′ (PE.trans (wk-id _) (PE.sym F≡F₁)) [F]₁
      [x∷F] = neuTerm ([F] (step id) (⊢Γ ∙ ⊢F)) (var 0) (var (⊢Γ ∙ ⊢F) here)
                      (proof-irrelevance (var (⊢Γ ∙ ⊢F) here) (var (⊢Γ ∙ ⊢F) here))
      [G]₁ = [G] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G]′ = PE.subst₂ (λ x y → _ ∙ y ^ _ ⊩⟨ _ ⟩ x ^ _)
                       (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                       (PE.sym F≡F₁) [G]₁
      [F≡H]₁ = [F≡F′] id ⊢Γ
      [F≡H]₁ = [F≡F′] id ⊢Γ
      [F≡H]′ = irrelevanceEq″ (PE.trans (wk-id _) (PE.sym F≡F₁))
                              (PE.trans (wk-id _) (PE.sym H≡F′))
                              PE.refl PE.refl 
                              [F]₁ [F]′ [F≡H]₁
      [G≡E]₁ = [G≡G′] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G≡E]′ = irrelevanceEqLift″ (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                                   (PE.trans (wkSingleSubstId _) (PE.sym E≡G′))
                                   (PE.sym F≡F₁) [G]₁ [G]′ [G≡E]₁
  in  PE.subst (λ r → Γ ⊢ _ ≡ _ ^ [ r , ι lF₁ ] ) (PE.sym rF≡rF₁)
        (PE.subst (λ l → Γ ⊢ F₁ ≡ H ^ [ % , l ] ) (PE.cong ι (PE.sym lF≡lF₁))
          (escapeEq [F]′ [F≡H]′)) ,
     ( PE.trans rF≡rF₁ (PE.sym rH≡rF′) ,
     ( PE.trans lF≡lF₁ (PE.sym lH≡lF′) ,
     ( PE.trans lG≡lG₁ (PE.sym lE≡lG′) ,
        PE.subst (λ r → (_ ∙ _ ^ [ r , _ ] ) ⊢ _ ≡ _ ^ _) (PE.sym rF≡rF₁)
         (PE.subst (λ l → (Γ ∙ F₁  ^ [ % , ι lF₁ ] ) ⊢ G₁ ≡ E ^ [ ! , l ]) (PE.cong ι  (PE.sym lG≡lG₁))
          (PE.subst (λ l → (Γ ∙ F₁ ^ [ % , l ] ) ⊢ G₁ ≡ E ^  [ ! , ι lG ]) (PE.cong ι (PE.sym lF≡lF₁))
           (escapeEq [G]′ [G≡E]′))))))

injectivity′ (emb emb< x) [ΠFG≡ΠHE] = injectivity′ x [ΠFG≡ΠHE]
injectivity′ (emb ∞< x) [ΠFG≡ΠHE] = injectivity′ x [ΠFG≡ΠHE]


-- Injectivity of Π
injectivity : ∀ {Γ F G H E rF lF lH lG lE rH lΠ} →
              Γ ⊢ Π F ^ rF ° lF ▹ G ° lG ° lΠ ^ ! ≡ Π H ^ rH ° lH ▹ E ° lE ° lΠ ^ ! ^ [ ! , ι lΠ ]
            → Γ ⊢ F ≡ H ^ [ rF , ι lF ]
            × rF PE.≡ rH
            × lF PE.≡ lH
            × lG PE.≡ lE
            × Γ ∙ F ^ [ rF , ι lF ] ⊢ G ≡ E ^ [ ! , ι lG ]
injectivity ⊢ΠFG≡ΠHE =
  let [ΠFG] , _ , [ΠFG≡ΠHE] = reducibleEq ⊢ΠFG≡ΠHE
  in  injectivity′ (Π-elim [ΠFG])
                   (irrelevanceEq [ΠFG] (Π-intr (Π-elim [ΠFG])) [ΠFG≡ΠHE])

{-injectivity-irr : ∀ {Γ F G H E rF lF lH rH} →
              Γ ⊢ Π F ^ rF ° lF ▹ G ° ⁰ ° ⁰ ≡ Π H ^ rH ° lH ▹ E ° ⁰ ° ⁰ ^ [ % , ι ⁰ ]
            → Γ ⊢ F ≡ H ^ [ rF , ι lF ]
            × rF PE.≡ rH
            × lF PE.≡ lH
            × Γ ∙ F ^ [ rF , ι lF ] ⊢ G ≡ E ^ [ % , ι ⁰ ]
injectivity-irr ⊢ΠFG≡ΠHE =
  let [ΠFG] , _ , [ΠFG≡ΠHE] = reducibleEq ⊢ΠFG≡ΠHE
  in  injectivity′ (Πirr-elim [ΠFG])
                   (irrelevanceEq [ΠFG] (Πirr-intr (Πirr-elim [ΠFG])) [ΠFG≡ΠHE])
-}

Uinjectivity′ : ∀ {Γ r₁ r₂ l₁ l₂ lU l}
               ([U] : Γ ⊩⟨ l ⟩U Univ r₁ l₁ ^ lU)
             → Γ ⊩⟨ l ⟩ Univ r₁ l₁ ≡ Univ r₂ l₂ ^ [ ! , lU ] / U-intr [U]
             → r₁ PE.≡ r₂ × l₁ PE.≡ l₂ × next l₁ PE.≡ lU
Uinjectivity′ (noemb (Uᵣ r l′ l< eq d)) D =
  let A , B = Univ-PE-injectivity (whnfRed* D Uₙ) 
      A' , B' = Univ-PE-injectivity (whnfRed* (red d) Uₙ)
  in (PE.trans A' (PE.sym A)) , (PE.trans B' (PE.sym B)) , PE.trans (PE.cong next B') eq
Uinjectivity′ (emb emb< a) b = Uinjectivity′ a b
Uinjectivity′ (emb ∞< a) b = Uinjectivity′ a b


Uinjectivity : ∀ {Γ r₁ r₂ l₁ l₂ lU} →
                 Γ ⊢ Univ r₁ l₁ ≡ Univ r₂ l₂  ^ [ ! , lU ] →
                 r₁ PE.≡ r₂ × l₁ PE.≡ l₂ × next l₁ PE.≡ lU
Uinjectivity ⊢U≡U =
  let [U] , _ , [U≡U] = reducibleEq ⊢U≡U
  in Uinjectivity′ (U-elim [U]) (irrelevanceEq [U] (U-intr (U-elim [U])) [U≡U])


-- injectivity of ∃

{-

∃injectivity′ : ∀ {F G H E Γ l}
               ([∃FG] : Γ ⊩⟨ l ⟩∃ (∃ F ▹ G) )
               ([F] : ∀ {ρ Δ} → ρ ∷ Δ ⊆ Γ → (⊢Δ : ⊢ Δ) → Δ ⊩⟨ l ⟩ U.wk ρ F ^ [ % , ι ⁰ ])
               ([G] : ∀ {ρ Δ a} → ([ρ] : ρ ∷ Δ ⊆ Γ) (⊢Δ : ⊢ Δ)
                      → Δ ⊩⟨ l ⟩ a ∷ U.wk ρ F ^ [ % , ι ⁰ ] / [F] [ρ] ⊢Δ
                      → Δ ⊩⟨ l ⟩ U.wk (lift ρ) G [ a ] ^ [ % , ι ⁰ ])
              ([F≡F′] : ∀ {ρ Δ}
               → ([ρ] : ρ ∷ Δ ⊆ Γ) (⊢Δ : ⊢ Δ)
               → Δ ⊩⟨ l ⟩ U.wk ρ F ≡ U.wk ρ H ^ [ % , ι ⁰ ] / [F] [ρ] ⊢Δ)
              ([G≡G′] : ∀ {ρ Δ a}
               → ([ρ] : ρ ∷ Δ ⊆ Γ) (⊢Δ : ⊢ Δ)
               → ([a] : Δ ⊩⟨ l ⟩ a ∷ U.wk ρ F ^ [ % , ι ⁰ ] / [F] [ρ] ⊢Δ)
               → Δ ⊩⟨ l ⟩ U.wk (lift ρ) G [ a ] ≡ U.wk (lift ρ) H [ a ] ^ [ % , ι ⁰ ] / [G] [ρ] ⊢Δ [a])
             → Γ ⊩⟨ l ⟩ ∃ F ▹ G ≡ ∃ H ▹ E ^ [ % , ι ⁰ ] / ∃-intr [∃FG]
             → Γ ⊢ F ≡ H ^ [ % , ι ⁰ ]
             × Γ ∙ F ^ [ % , ι ⁰ ] ⊢ G ≡ E ^ [ % , ι ⁰ ]
∃injectivity′ {F₁} {G₁} {H} {E} {Γ = Γ} 
         _ [F] [G] [F≡F′] [G≡G′]
         (∃₌ F′ G′ D′ A≡B) =
  let F≡F₁ , G≡G₁ = ∃-PE-injectivity (whnfRed* (red D) ∃ₙ)
      H≡F′ , E≡G′ = ∃-PE-injectivity (whnfRed* D′ ∃ₙ)
      ⊢Γ = wf ⊢F
      [F]₁ = [F] id ⊢Γ
      [F]′ = irrelevance′ (PE.trans (wk-id _) (PE.sym F≡F₁)) [F]₁
      [x∷F] = neuTerm ([F] (step id) (⊢Γ ∙ ⊢F)) (var 0) (var (⊢Γ ∙ ⊢F) here) (proof-irrelevance (var (⊢Γ ∙ ⊢F) here) (var (⊢Γ ∙ ⊢F) here))  
      [G]₁ = [G] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G]′ = PE.subst₂ (λ x y → _ ∙ y ^ _ ⊩⟨ _ ⟩ x ^ _)
                       (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                       (PE.sym F≡F₁) [G]₁
      [F≡H]₁ = [F≡F′] id ⊢Γ
      [F≡H]′ = irrelevanceEq″ (PE.trans (wk-id _) (PE.sym F≡F₁))
                              (PE.trans (wk-id _) (PE.sym H≡F′))
                              PE.refl PE.refl 
                              [F]₁ [F]′ [F≡H]₁
      [G≡E]₁ = [G≡G′] (step id) (⊢Γ ∙ ⊢F) [x∷F]
      [G≡E]′ = irrelevanceEqLift″ (PE.trans (wkSingleSubstId _) (PE.sym G≡G₁))
                                   (PE.trans (wkSingleSubstId _) (PE.sym E≡G′))
                                   (PE.sym F≡F₁) [G]₁ [G]′ [G≡E]₁
  in escapeEq [F]′ [F≡H]′ , escapeEq [G]′ [G≡E]′
∃injectivity′ (emb emb< x) [F] [G] [F≡F′] [G≡G′] [∃FG≡∃HE] = ∃injectivity′ x [F] [G] [F≡F′] [G≡G′] [∃FG≡∃HE]
∃injectivity′ (emb ∞< x) [F] [G] [F≡F′] [G≡G′] [∃FG≡∃HE] = ∃injectivity′ x [F] [G] [F≡F′] [G≡G′] [∃FG≡∃HE]

-- Injectivity of ∃
∃injectivity : ∀ {Γ F G H E l∃} →
              Γ ⊢ ∃ F ▹ G ≡ ∃ H ▹ E ^ [ % , ι l∃ ]
            → Γ ⊢ F ≡ H ^ [ % , ι l∃ ]
            × Γ ∙ F ^ [ % , ι l∃ ] ⊢ G ≡ E ^ [ % , ι l∃ ]
∃injectivity ⊢∃FG≡∃HE =
  let [∃FG] , _ , [∃FG≡∃HE] = reducibleEq ⊢∃FG≡∃HE
  in ∃injectivity′ (∃-elim [∃FG])
                   (irrelevanceEq [∃FG] (∃-intr (∃-elim [∃FG])) [∃FG≡∃HE])

-}
